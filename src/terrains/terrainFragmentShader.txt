#version 400 core

const int maxLights = 10;

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[maxLights];
in vec3 toCameraVector;
in float visibility;
in vec4 shadowCoords;

out vec4 out_Color;

uniform sampler2D backgroundTexture;
uniform sampler2D rTexture;
uniform sampler2D gTexture;
uniform sampler2D bTexture;
uniform sampler2D blendMap;
uniform sampler2D shadowMap;

uniform vec3 lightColor[maxLights];
uniform vec3 coneDirection[maxLights];
uniform float coneAngle[maxLights];
uniform vec3 attenuation[maxLights];
uniform float shineDampers[4];
uniform float reflectivitys[4];
uniform vec3 skyColor;

const int pcfCount = 2;
const float totalTexels = (pcfCount * 2.0 + 1.0) * (pcfCount * 2.0 + 1.0);

void main(void){
	
	float mapSize = 4096.0;
	float texelSize = 1.0 / mapSize;
	float totalTexelsInside = 0.0;
	
	for(int x = -pcfCount; x <= pcfCount; x++){
		for(int y = -pcfCount; y <= pcfCount; y++){
			float objectNearestLight = texture(shadowMap, shadowCoords.xy + vec2(x, y) * texelSize).r;
			if(shadowCoords.z > objectNearestLight){
				totalTexelsInside += 1.0;
			}
		}
	}
	
	totalTexelsInside /= totalTexels;
	
	float lightFactor = 1.0 - (totalTexelsInside * shadowCoords.w);
	
	vec4 blendMapColor = texture(blendMap, pass_textureCoords);
	
	float backTextureAmount = 1 - (blendMapColor.r + blendMapColor.g + blendMapColor.b);
	vec2 tiledCoords = pass_textureCoords * 40.0;
	vec4 backgroundTextureColor = texture(backgroundTexture, tiledCoords) * backTextureAmount;
	vec4 rTextureColor = texture(rTexture, tiledCoords) * blendMapColor.r;
	vec4 gTextureColor = texture(gTexture, tiledCoords) * blendMapColor.g;
	vec4 bTextureColor = texture(bTexture, tiledCoords) * blendMapColor.b;
	
	float totalReflectivity = reflectivitys[0] * backTextureAmount;
	totalReflectivity += reflectivitys[1] * blendMapColor.r;
	totalReflectivity += reflectivitys[2] * blendMapColor.g;
	totalReflectivity += reflectivitys[3] * blendMapColor.b;
	
	float totalDamping = shineDampers[0] * backTextureAmount;
	totalDamping += shineDampers[1] * blendMapColor.r;
	totalDamping += shineDampers[2] * blendMapColor.g;
	totalDamping += shineDampers[3] * blendMapColor.b;
	
	
	vec4 totalColor = backgroundTextureColor + rTextureColor + gTextureColor + bTextureColor;
	
	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitVectorToCamera = normalize(toCameraVector);
	
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	
	for(int i = 0; i < maxLights; i++){
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
		vec3 unitLightVector = normalize(toLightVector[i]);
		float nDot1 = dot(unitNormal, unitLightVector);
		float brightness = max(nDot1, 0.0);
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);
		float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
		specularFactor = max(specularFactor, 0.0);
		float dampedFactor = pow(specularFactor, totalDamping);
		
		vec3 diffuse = (brightness * lightColor[i]) / attFactor;
		vec3 specular = (dampedFactor * totalReflectivity * lightColor[i]) / attFactor;
		
		float toLightAngle = degrees(acos(dot(-normalize(toLightVector[i]), normalize(coneDirection[i]))));
		if( toLightAngle < coneAngle[i] ){
			float amount = (coneAngle[i] - toLightAngle);
			amount = clamp(amount, 0.0 , 25.0);
			amount /= 25;
			totalDiffuse = totalDiffuse + diffuse * amount;
			totalSpecular = totalSpecular + specular * amount;
		}
	}
	totalDiffuse = max(totalDiffuse * lightFactor, 0.15);
	
	out_Color = vec4(totalDiffuse, 1.0) * totalColor + vec4(totalSpecular, 1.0);
	out_Color = mix(vec4(skyColor, 1.0), out_Color, visibility);
}